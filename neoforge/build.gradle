plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${BuildConfig.neoforgeLoaderVersion}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
                license: BuildConfig.license,
                modId: BuildConfig.modId,
                version: BuildConfig.modVersion,
                modName: BuildConfig.modName,
                authors: BuildConfig.modAuthors.join(", "),
                modDescription: BuildConfig.modDescription,
                minecraftVersion: BuildConfig.minecraftVersion,
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
    atAccessWideners.add("${BuildConfig.modId}.classtweaker") // this is a path inside the mod jar
}
